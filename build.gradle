plugins {
  id "org.jetbrains.kotlin.jvm" version "1.3.20"

  id "groovy" //Needed for Spock tests

  id 'org.springframework.boot' version '2.1.2.RELEASE'
  id "org.jetbrains.kotlin.plugin.spring" version "1.3.20"

  id "com.mttnow.build" version '1.2.0'
}

group = 'com.travelport.service.glossary'
description = 'Glossary of Applications, Libraries and Applications developed'

sourceCompatibility = JavaVersion.VERSION_1_8

compileKotlin {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8

  kotlinOptions {
    jvmTarget = "1.8"
  }
}

build {
  sonarAnalysisExclusions = ['**/Application.groovy', '**/model/**', '**/*Mock*.groovy', '**/*Test*.groovy', '**/*Test.groovy', '**/*IT.groovy', '**/RunCukes.groovy']
  sonarCodeCoverageExclusions = sonarAnalysisExclusions
}

ext {
  mttJsonObjectMapper = "1.3.0"
  mttPlatformCommonLoggingVersion = "1.2.2"

  autoConfigureVersion = "2.3.0"
  testAutoConfigureVersion = "2.2.0"
  rabbitAutoConfigureVersion = "1.1.0"

  spockVersion = "1.2-groovy-2.4-SNAPSHOT"
  spockCollaboratorVersion = "1.2.1"

  cukesVersion = "4.0.0"
//  jaywayRestAssuredVersion = '3.1.1'
  jaywayRestAssuredVersion = '2.4.1'
}

dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

  //Enable hot reloading in IDE. Make sure you turn on auto conpile on IntelliJ also! See 'https://stackoverflow.com/questions/23155244/spring-boot-hotswap-with-intellij-ide'
  compile("org.springframework.boot:spring-boot-devtools")

  compile "org.springframework.boot:spring-boot-starter-actuator"
  compile "org.springframework.boot:spring-boot-starter-web"
  compile "org.springframework.boot:spring-boot-starter-data-mongodb"
  compile "org.springframework.boot:spring-boot-starter-amqp"
  compile("org.springframework.boot:spring-boot-starter-hateoas") {
    exclude group: "org.springframework", module: "spring-aop"
    exclude group: "org.springframework", module: "spring-beans"
    exclude group: "org.springframework", module: "spring-context"
    exclude group: "org.springframework", module: "spring-core"
    exclude group: "org.springframework", module: "spring-web"
    exclude group: "org.springframework", module: "spring-webmvc"
    exclude group: "org.slf4j", module: "slf4j-api"
  }
  compile("org.springframework.boot:spring-boot-starter-thymeleaf")

  compile "com.mttnow.platform.spring.boot.auto.configure:platform-spring-boot-auto-configure:${autoConfigureVersion}"
  compile "com.mttnow.common:json-object-mapper:${mttJsonObjectMapper}"
  compile "com.mttnow.platform.common:logging-java:${mttPlatformCommonLoggingVersion}"

  compile("com.fasterxml.jackson.module:jackson-module-kotlin:2.9.2")

  compile "org.yaml:snakeyaml:1.23"

  testCompile "org.springframework.boot:spring-boot-starter-test"
  testCompile "de.flapdoodle.embed:de.flapdoodle.embed.mongo"

  testCompile "io.cucumber:cucumber-groovy:${cukesVersion}"
  testCompile "io.cucumber:cucumber-junit:${cukesVersion}"
//  testCompile "io.rest-assured:rest-assured:${jaywayRestAssuredVersion}"
  testCompile "com.jayway.restassured:rest-assured:${jaywayRestAssuredVersion}"

  testCompile "com.mttnow.platform.spring.boot.test.auto.configure:platform-spring-boot-test-auto-configure:${testAutoConfigureVersion}"
  testCompile "com.mttnow.platform.spring.boot.test.auto.configure.rabbit:platform-spring-boot-test-auto-configure-rabbit:${rabbitAutoConfigureVersion}"

  testCompile "org.codehaus.groovy:groovy"
  testCompile "org.spockframework:spock-core:${spockVersion}"
  testCompile "org.spockframework:spock-spring:${spockVersion}"
  testCompile "com.blogspot.toomuchcoding:spock-subjects-collaborators-extension:${spockCollaboratorVersion}"
}

sonarqube {
  properties {
    property 'sonar.groovy.jacoco.reportPath', "${buildDir}/jacoco/unit-tests.exec"
    property 'sonar.groovy.jacoco.itReportPath', "${buildDir}/jacoco/integration-tests.exec"
  }
}

test {
  exclude '**/acceptance/**'
  exclude "**/integration/**"

  jacoco {
    destinationFile = file("${buildDir}/jacoco/unit-tests.exec")
  }
}

task testIntegration(type: Test) {
  description = 'Runs Integration Tests'

  include "com/travelport/**/integration/**"

  jacoco {
    destinationFile = file("${buildDir}/jacoco/integration.exec")
  }

  mustRunAfter test
}

task testAcceptanceLocal(type: Test) {
  description = 'Runs Local Acceptance Tests'

  include '**/RunCukes.class'

  jacoco {
    destinationFile = file("${buildDir}/jacoco/acceptance-local.exec")
  }

  mustRunAfter testIntegration
}

task mergeIntegrationAndAcceptanceTestCoverage(type: JacocoMerge) {
  executionData testIntegration, testAcceptanceLocal
  destinationFile = file("${buildDir}/jacoco/integration-tests.exec")

  mustRunAfter testAcceptanceLocal
}

stageTest.dependsOn(testIntegration, testAcceptanceLocal, mergeIntegrationAndAcceptanceTestCoverage)
